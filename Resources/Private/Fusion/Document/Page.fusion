prototype(Theme.Blog.Plain:Document.Page) < prototype(Neos.Neos:Page) {
  htmlTag.attributes {
    lang = 'en'
  }
  head {
    viewportMetaTag = Neos.Fusion:Tag {
      tagName = 'meta'
      attributes {
        name = 'viewport'
        content = 'width=device-width, user-scalable=no, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0'
      }
      @position = 'after titleTag'
    }

    favicon = Neos.Fusion:Component {
      icon = Neos.Neos:ImageUri {
        asset = ${q(site).property('logo')}
        maxWidth = 16
        maxHeight = 16
        allowCropping = true
        format = 'png'
      }
      renderer = afx`
        <link rel="shortcut icon" type="image/png" href={props.icon}/>
      `
    }

    stylesheets = Neos.Fusion:Component {
      styles = Neos.Fusion:ResourceUri {
        path = 'resource://Theme.Blog.Plain/Public/styles/main.css'
      }

      renderer = afx`
        <link rel="stylesheet" href={props.styles}/>
      `
    }

    javascripts = Neos.Fusion:Component {
      renderer = afx`
        <script src="https://unpkg.com/feather-icons"></script>
        <script src="https://unpkg.com/axios/dist/axios.min.js"></script>
      `
    }
  }

  body = Neos.Fusion:Component {

    breadcrumbs = Theme.Blog.Plain:Prototype.Breadcrumbs


    content = Neos.Neos:PrimaryContent {
      nodePath = "main"
    }

    # Rendering
    renderer = afx`
      <button data-toggle="sidebar" class="sidebar__handle"><i data-feather="menu"></i></button>
      <aside class="sidebar" data-identifier="sidebar">
        <Theme.Blog.Plain:Prototype.Profile/>
        <Theme.Blog.Plain:Prototype.Menu/>
        <Theme.Blog.Plain:Prototype.Follow/>
      </aside>
      <main class="main" role="main">
        {props.breadcrumbs}
        {props.content}
      </main>
      <footer class="footer">
        <Theme.Blog.Plain:Prototype.Footer/>
      </footer>
    `

    javascripts = Neos.Fusion:Component {
      main = Neos.Fusion:ResourceUri {
        path = 'resource://Theme.Blog.Plain/Public/scripts/main.js'
      }
      renderer = afx`
        <script src={props.main}></script>
        <script async src="https://static.addtoany.com/menu/page.js"></script>
      `
    }
  }
}
