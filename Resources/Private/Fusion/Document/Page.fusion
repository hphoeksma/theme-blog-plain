prototype(Theme.Blog.Plain:Document.Page) < prototype(Neos.Neos:Page) {
  htmlTag.attributes {
    lang = 'en'
  }
  head {
    viewportMetaTag = Neos.Fusion:Tag {
      tagName = 'meta'
      attributes {
        name = 'viewport'
        content = 'width=device-width, user-scalable=no, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0'
      }
      @position = 'after titleTag'
    }

    favicon = Neos.Fusion:Component {
      icon = Neos.Neos:ImageUri {
        asset = ${q(site).property('logo')}
        maxWidth = 16
        maxHeight = 16
        allowCropping = true
        format = 'png'
      }
      renderer = afx`
        <link rel="shortcut icon" type="image/png" href={props.icon}/>
      `
    }

    stylesheets {
      default = Neos.Fusion:Component {
        main = Neos.Fusion:ResourceUri {
          path = 'resource://Theme.Blog.Plain/Public/styles/main.css'
        }

        renderer = afx`
          <link rel="stylesheet" href={props.main}/>
        `
      }
    }

  }

  body = Neos.Fusion:Component {

    breadcrumbs = Theme.Blog.Plain:Prototype.Breadcrumbs

    content = Neos.Neos:PrimaryContent {
      default.renderer {
        tagName = 'section'
        attributes.class = 'primary-content'
      }
      nodePath = "main"
    }

    # Rendering
    renderer = afx`
      <div class="container">
        <main class="main" role="main">
          <button data-toggle="sidebar" class="sidebar__handle">
            <svg
              xmlns="http://www.w3.org/2000/svg"
              width="24"
              height="24"
              viewbox="0 0 24 24"
              fill="none"
              stroke="currentColor"
              stroke-width="2"
              stroke-linecap="round"
              stroke-linejoin="round"
              class="feather feather-menu"
            >
              <line x1="3" y1="12" x2="21" y2="12"></line>
              <line x1="3" y1="6" x2="21" y2="6"></line>
              <line x1="3" y1="18" x2="21" y2="18"></line>
            </svg>
          </button>
          {props.breadcrumbs}
          {props.content}
        </main>
        <footer class="footer">
          <Theme.Blog.Plain:Prototype.Footer/>
        </footer>
      </div>
      <aside class="sidebar" data-identifier="sidebar">
        <Theme.Blog.Plain:Prototype.Profile/>
        <Theme.Blog.Plain:Prototype.Menu/>
        <Theme.Blog.Plain:Prototype.Follow/>
      </aside>
    `

    javascripts {
      blog = Neos.Fusion:Component {
        main = Neos.Fusion:ResourceUri {
          path = 'resource://Theme.Blog.Plain/Public/scripts/main.js'
        }
        renderer = afx`
          <script src={props.main}></script>
          <script async src="https://static.addtoany.com/menu/page.js"></script>
        `
      }
    }
  }
}
