prototype(Theme.Blog.Plain:Document.Post) < prototype(Theme.Blog.Plain:Document.Page) {
  # Reset Primary Content
  body.content >
  # Reassign it to the post article prototype
  body.content = Theme.Blog.Plain:Prototype.Post
}

prototype(Theme.Blog.Plain:Prototype.Post) < prototype(Neos.Fusion:Component) {
  @context.showPermalink = false
  header = ${q(documentNode).property('title')}
  content = Neos.Neos:ContentCollection {
    nodePath = 'main'
  }
  publicationDate = ${Date.format(q(documentNode).property('publicationDateTime'), 'd-m-Y')}
  intenseDebateSiteAcct = ${Configuration.setting('Theme.Blog.Plain.commenting.intenseDebateSiteAcct')}
  disqusEmbed = ${Configuration.setting('Theme.Blog.Plain.commenting.disqusEmbed')}
  enableCommenting = ${q(documentNode).property('enableCommenting') && (this.intenseDebateSiteAcct || this.disqusEmbed)}
  nodeUri = Neos.Neos:NodeUri {
    absolute = true
    node = ${documentNode}
  }
  node = ${node}

  # Rendering
  renderer = afx`
    <article class="blog__article">
      <h1 class="article__header">{props.header}</h1>
      {props.content}
      <footer class="article__footer">
        <p class="article__meta">
          {Translation.translate('post.publicationDateLead', null, [], 'NodeTypes/Document/Post', 'Theme.Blog.Plain')}
          <span class="article__publicationDate">{props.publicationDate}</span>
        </p>
        <a href={props.nodeUri} class="article__permalink" @if.isHome={showPermalink}>{Translation.translate('post.permalinkText',
          null, [], 'NodeTypes/Document/Post', 'Theme.Blog.Plain')}</a>
        <Theme.Blog.Plain:Prototype.ShareLinks documentNode={documentNode}/>
      </footer>
    </article>
    <div class="blog__commenting" @if.hasCommenting={props.enableCommenting}>
      <div @if.hasIntenseDebate={props.intenseDebateSiteAcct}>
        <script>
          var idcomments_acct = '{props.intenseDebateSiteAcct}';
          var idcomments_post_id;
          var idcomments_post_url = '{props.nodeUri}';
        </script>
        <span id="IDCommentsPostTitle" style="display:none"></span>
        <script type='text/javascript' src='https://www.intensedebate.com/js/genericCommentWrapperV2.js'></script>
      </div>
      <div id="disqus_thread" data-identifier={props.node.identifier} data-embed={props.disqusEmbed}
           data-uri={props.nodeUri}
           @if.hasDisqus={props.disqusEmbed}></div>
    </div>
  `
}
