##
# "Text" element
#
prototype(Theme.Blog.Plain:Content.PostIndex) < prototype(Neos.Fusion:Component) {
  posts = ${q(site).find('[instanceof Theme.Blog.Plain:Document.Post]').sort('publicationDateTime', 'DESC').get()}

  renderer = afx`
    <div class="blog__index">
      <Neos.Fusion:Loop items={props.posts}>
        <Theme.Blog.Plain:ProtoType.PostIndexItem node={item}/>
      </Neos.Fusion:Loop>
    </div>
  `

  @cache {
    mode = 'cached'
    entryTags {
      1 = ${Neos.Caching.nodeTag(node)}
      2 = ${Neos.Caching.nodeTypeTag('Theme.Blog.Plain:Document.Post', node)}
    }
  }
}

prototype(Theme.Blog.Plain:ProtoType.PostIndexItem) < prototype(Neos.Fusion:Component) {
  @context.node = ${this.node}

  imageUri = Neos.Neos:ImageUri {
    asset = ${q(node).children('main').find('[instanceof Theme.Blog.Plain:Content.Image]').first().property('image')}
  }

  nodeUri = Neos.Neos:NodeUri {
    node = ${node}
  }

  intro = ${q(node).children('main').find('[instanceof Theme.Blog.Plain:Content.Lead]').get() ? q(node).children('main').find('[instanceof Theme.Blog.Plain:Content.Lead]').first().property('text') : q(node).children('main').find('[instanceof Theme.Blog.Plain:Content.Text],[instanceof Theme.Blog.Plain:Content.TextWithImage]').first().property('text')}
  intro.@process.maxLength = ${String.crop(String.stripTags(value), 200, '&hellip;')}
  class = Neos.Fusion:Join {
    1 = "article__header"
    2 = "has-gradient"
    2.@if.hasImage = ${imageUri}
    @glue = ' '
    @process.trimWhiteSpace = ${String.trim(value)}
  }

  renderer = afx`
    <article class="article">
      <Neos.Fusion:Tag tagName="a" attributes.href={props.nodeUri} attributes.title={node.title}
                       attributes.class={props.class} attributes.data-image={props.imageUri}>
        <h1>{node.label}</h1>
      </Neos.Fusion:Tag>
      <p class="article__intro">
        {props.intro}
      </p>
      <a href={props.nodeUri} class="article__read-more">
        <span>continue reading</span>
        <svg xmlns="http://www.w3.org/2000/svg" width="24" height="24" viewBox="0 0 24 24" fill="none"
             stroke="currentColor" stroke-width="2" stroke-linecap="round" stroke-linejoin="round"
             class="feather feather-arrow-right">
          <line x1="5" y1="12" x2="19" y2="12"></line>
          <polyline points="12 5 19 12 12 19"></polyline>
        </svg>
      </a>
    </article>
  `
}
